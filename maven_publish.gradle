apply plugin: 'maven'

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "https://maven.gcx.org/") {
                authentication(userName: gcxMavenRepositoryUsername, password: gcxMavenRepositoryPassword)
            }
            snapshotRepository(url: "https://maven.gcx.org/") {
                authentication(userName: gcxMavenRepositoryUsername, password: gcxMavenRepositoryPassword)
            }
        }
    }
}

afterEvaluate {
    if (project.plugins.hasPlugin('java')) {
        task artifactSourceJar(type: Jar) {
            from sourceSets.main.allSource
            classifier = 'sources'
        }

        task artifactJavadocsJar(type: Jar, dependsOn: 'javadoc') {
            from javadoc.destinationDir
            classifier = 'javadoc'
        }
    } else if (project.plugins.hasPlugin('com.android.library')) {
        apply plugin: 'android-maven'

        task artifactSourceJar(type: Jar) {
            classifier = 'sources'
            from android.sourceSets.main.java.srcDirs
        }

        task javadoc(type: Javadoc) {
            source = android.sourceSets.main.java.srcDirs
            classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        }

        task artifactJavadocsJar(type: Jar, dependsOn: 'javadoc') {
            classifier = 'javadoc'
            from javadoc.destinationDir
        }
    }
}
